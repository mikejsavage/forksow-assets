if %SHOW_SPEED > 0
if %SPEED != #NOTSET

if %RACE == 0
setSize 10 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 10
setColor 1 1 1 1
setAlign 2 2
setCursor #WIDTH / 2, #HEIGHT - 5
drawNum %SPEED
endif

if %RACE // ported from racesow's joki
// ------------------ BEGIN of draw speed bar ----------------------
// grows with horizontal resolution (#SCALEBYWIDTH)
setCursor 296, 400
// speed colors: 
// 450-750: gray 1, 1, 1, 0.5
// 751-1000: dark-green 0, 0.5, 0
// 1001-1250: cyan 0, 1, 1
// 1251-1500: blue 0, 0, 1
// 1501-1750: purple 1, 0, 1
// 1751-2000: orange 1, 0.5, 0
// 2001-3000: red 1, 0, 0
// 3001-4000: black 0, 0, 0

// draw background
setAlign #LEFT, #MIDDLE
setScale #SCALEBYWIDTH
setSize 208, 24
setColor 0.2, 0.2, 0.2, 0.2
drawPicByName gfx/ui/white

// draw overlaying speed bars
moveCursor 4, 0
setAlign #LEFT, #MIDDLE
setSize 200, 16

// draw speedbar (321-600)
if %SPEED > 320 
if %SPEED <= 600
    setColor 1, 1, 1, 0.5
    drawBar %SPEED - 320, 280
endif
endif

// draw speedbar dark-green (600-1000)
if %SPEED > 600
if %SPEED <= 1000
    setColor 1, 1, 1, 2 - %SPEED * 0.002
    drawPicByName gfx/ui/white
    setColor 0, 0.5, 0, 1
    drawBar %SPEED - 600, 400
endif
endif

// draw speedbar cyan (1001-1500)
if %SPEED > 1000
if %SPEED <= 1500
    setColor 0, 0.5, 0, 5 - %SPEED * 0.004
    drawPicByName gfx/ui/white
    setColor 0, 1, 1, 1
    drawBar %SPEED - 1000, 500
endif
endif

// 1251-1500: blue 0, 0, 1 (1501-2000)
if %SPEED > 1500
if %SPEED <= 2000
    setColor 0, 1, 1, 6 - %SPEED * 0.004
    drawPicByName gfx/ui/white
    setColor 0, 0, 1, 1
    drawBar %SPEED - 1500, 250
endif
endif

// 1501-1750: purple 1, 0, 1 (2001-2500)
if %SPEED > 2000
if %SPEED <= 2500
    setColor 0, 0, 1, 7 - %SPEED * 0.004
    drawPicByName gfx/ui/white
    setColor 1, 0, 1, 1
    drawBar %SPEED - 2000, 500
endif
endif

// 1751-2000: orange 1, 0, 0.5 (2501-3000)
if %SPEED > 2500
if %SPEED <= 3000
    setColor 1, 0, 1, 8 - %SPEED * 0.004
    drawPicByName gfx/ui/white
    setColor 1, 0.5, 0, 1
    drawBar %SPEED - 2500, 500
endif
endif

// draw speedbar red (3001-3500)
if %SPEED > 3000
if %SPEED <= 3500
    setColor 1, 0.5, 0, 3 - 0.001 * %SPEED
    drawPicByName gfx/ui/white
    setColor 1, 0, 0, 1
    drawBar %SPEED - 3000, 500
endif
endif

// draw speedbar black (3501-4000)
if %SPEED > 3500
    setColor 1, 0, 0, 4 - 0.001 * %SPEED
    drawPicByName gfx/ui/white
    setColor 0, 0, 0, 1
    drawBar %SPEED - 3500, 500
endif

setScale #DEFAULTSCALE //resets scale
// ------------------ END of draw speed bar ----------------------

if %RACE || %SHOW_SPEED == 2	//alternative speed position
    setAlign 2 2
    setSize 16 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 16
    setColor 1 1 1 0.85
    setCursor #WIDTH / 2, #HEIGHT - 200
    drawNum %SPEED
endif

endif //if %RACE
endif //if %SPEED != #NOTSET
endif //if %SHOW_SPEED > 0
