setCursor #WIDTH / 2, 0
setAlign #CENTER, #TOP
setColor 0, 0, 0, 0.5

if %MATCH_STATE < #MATCH_STATE_PLAYTIME
	// background
	setSize 120 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 32
	drawPicByName $whiteimage

	moveCursor 0, 15
	setAlign #CENTER, #MIDDLE
	setFontSize %VIDHEIGHT / 64
	setFontStyle bold
	setColor 1, 1, 1, 1
	drawString "WARMUP"
	setFontStyle normal
endif

if %MATCH_STATE == #MATCH_STATE_PLAYTIME
	// background
	setSize 120 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 32
	if %MESSAGE_OTHER > 0
		setSize 120 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 48
	endif
	drawPicByName $whiteimage

	// clock
	setColor 1, 1, 1, 1
	setFontSize %VIDHEIGHT / 32
	moveCursor 0, 3
	drawClock

	// scores
	setFontStyle bold

	if %TEAM_ALPHA_SCORE != #NOTSET
		moveCursor -40 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		setColorToTeamColor #TEAM_ALPHA
		drawStringNum %TEAM_ALPHA_SCORE

		moveCursor 40 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
	endif

	if %TEAM_BETA_SCORE != #NOTSET
		moveCursor 40 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		setColorToTeamColor #TEAM_BETA
		drawStringNum %TEAM_BETA_SCORE

		moveCursor -40 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
	endif

	// match point/overtime
	if %MESSAGE_OTHER != #RoundType_Normal
		moveCursor 0, 26
		setFontSize %VIDHEIGHT / 64
		setFontStyle bold
		setColor 1, 1, 1, 1
		if %MESSAGE_OTHER == #RoundType_MatchPoint
			drawString "MATCH POINT"
		endif
		if %MESSAGE_OTHER == #RoundType_Overtime
			drawString "OVERTIME"
		endif
		if %MESSAGE_OTHER == #RoundType_OvertimeMatchPoint
			drawString "OT MATCH POINT"
		endif
	endif

	// alive players
	setCursor #WIDTH / 2, 7
	setFontSize %VIDHEIGHT / 32
	setFontStyle normal

	setSpecialFontFamily "Silhouette"

	if %MESSAGE_ALPHA
		setAlign #RIGHT, #TOP
		moveCursor -70 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		moveCursor 1, 1
		setColor 0.2, 0.2, 0.2, 1
		drawStringRepeatConfigString "?" %MESSAGE2_ALPHA
		moveCursor -1 -1

		setColorToTeamColor #TEAM_ALPHA
		drawStringRepeatConfigString "?" %MESSAGE_ALPHA

		moveCursor 70 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
	endif

	if %MESSAGE_BETA
		setAlign #LEFT, #TOP
		moveCursor 70 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0

		moveCursor 1, 1
		setColor 0.2, 0.2, 0.2, 1
		drawStringRepeatConfigString "?" %MESSAGE2_BETA
		moveCursor -1 -1

		setColorToTeamColor #TEAM_BETA
		drawStringRepeatConfigString "?" %MESSAGE_BETA
	endif

	setFontFamily con_fontSystem
endif
