ifnot %CHAT_MODE && %SOFTKEYBOARD

setColor 1, 1, 1, 1

if %MATCH_STATE <= #MATCH_STATE_PLAYTIME
	if %REALTEAM != #TEAM_SPECTATOR
		if %HEALTH > 0
		if %PMOVE_TYPE == #PMOVE_TYPE_NORMAL
			if %TUTORIAL
			if %MATCH_STATE == #MATCH_STATE_WARMUP
			if %SPEED < 90
				// tutorial grip instructions
				setCursor #WIDTH * 0.5, 420
				setSize 360 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 90
				setColorAlpha 0.25
				if %SPEED > 50
					setColorAlpha 0.25 - 0.00625 * %SPEED - 50
				endif
				setAlign #CENTER, #BOTTOM
				drawPicByName gfx/hud/help_bg
				setAlign #CENTER, #TOP
				drawSubPicByName gfx/hud/help_bg 0 1 1 0
				setHeight 180
				setColorAlpha 1
				setAlign #CENTER, #MIDDLE
				if %SPEED > 50
					setColorAlpha 1 - 0.025 * %SPEED - 50
				endif
				drawSubPicByName gfx/ui/hands, 0.5 - 0.5 * %TOUCH_FLIP, 0, 0.5 + 0.5 * %TOUCH_FLIP, 1
			endif
			endif
			endif

			// attack
			setAlign #CENTER - %TOUCH_FLIP, #BOTTOM
			setCursor 0, 40
			if %TOUCH_FLIP > 0
				moveCursor #WIDTH, 0
			endif
			moveCursor %TOUCH_FLIP * -10 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
			moveCursor %TOUCH_FLIP * -256 * %TOUCH_SCALE * 0.01 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 128 * %TOUCH_SCALE * 0.01
			setSize #WIDTH, #HEIGHT // extend the area to the edges of the screen
			touchAttack
			ifnot %SCOREBOARD
				setSize 256 * %TOUCH_SCALE * 0.01 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 128 * %TOUCH_SCALE * 0.01
				setColorAlpha 0.3 + %TOUCH_ATTACK * 0.3
				if %TOUCH_FLIP > 0
					drawPicByName "gfx/hud/keys/act_fire_wide"
				endif
				if %TOUCH_FLIP < 0
					drawPicByName "gfx/hud/keys/act_fire_wide_flip"
				endif
			endif

			// jump
			setAlign #CENTER + %TOUCH_FLIP, #BOTTOM
			setCursor 0, 40
			if %TOUCH_FLIP < 0
				moveCursor #WIDTH, 0
			endif
			moveCursor %TOUCH_FLIP * 10 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
			moveCursor %TOUCH_FLIP * 256 * %TOUCH_SCALE * 0.01 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 128 * %TOUCH_SCALE * 0.01
			setSize #WIDTH, #HEIGHT // extend the area to the edges of the screen
			touchJump
			ifnot %SCOREBOARD
				setSize 256 * %TOUCH_SCALE * 0.01 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 128 * %TOUCH_SCALE * 0.01
				setColorAlpha 0.3
				if %TOUCH_UPMOVE > 0
					setColorAlpha 0.6
				endif
				if %TOUCH_FLIP < 0
					drawPicByName "gfx/hud/keys/act_jump_wide"
				endif
				if %TOUCH_FLIP > 0
					drawPicByName "gfx/hud/keys/act_jump_wide_flip"
				endif
			endif

			setSize 128 * %TOUCH_SCALE * 0.01 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 128 * %TOUCH_SCALE * 0.01

			// special
			setAlign #CENTER + %TOUCH_FLIP, #TOP
			setCursor 0, 40
			if %TOUCH_FLIP > 0
				moveCursor #WIDTH, 0
			endif
			moveCursor %TOUCH_FLIP * -10 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
			moveCursor %TOUCH_FLIP * -144 * %TOUCH_SCALE * 0.01 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 128 * %TOUCH_SCALE * 0.01
			touchSpecial
			ifnot %SCOREBOARD
				setColorAlpha 0.3 + %TOUCH_SPECIAL * 0.3
				if %TOUCH_FLIP > 0
					drawPicByName "gfx/hud/keys/act_special"
				endif
				if %TOUCH_FLIP < 0
					drawPicByName "gfx/hud/keys/act_special_flip"
				endif
			endif

			// crouch
			setAlign #CENTER - %TOUCH_FLIP, #TOP
			setCursor 0, 40
			if %TOUCH_FLIP < 0
				moveCursor #WIDTH, 0
			endif
			moveCursor %TOUCH_FLIP * 10 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 0
			moveCursor %TOUCH_FLIP * 144 * %TOUCH_SCALE * 0.01 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 128 * %TOUCH_SCALE * 0.01
			touchCrouch
			ifnot %SCOREBOARD
				setColorAlpha 0.3
				if %TOUCH_UPMOVE < 0
					setColorAlpha 0.6
				endif
				if %TOUCH_FLIP < 0
					drawPicByName "gfx/hud/keys/act_crouch"
				endif
				if %TOUCH_FLIP > 0
					drawPicByName "gfx/hud/keys/act_crouch_flip"
				endif
				if %SHOW_TOUCH_MOVEDIR
				if %TOUCH_MOVEDIR != #NOTSET
					setAlign #CENTER + %TOUCH_FLIP, #TOP
					setColorAlpha 0.3
					drawRotatedPicByName "gfx/hud/keys/touch_movedir", %TOUCH_MOVEDIR
				endif
				endif
			endif

			setSize #WIDTH, #HEIGHT
			setCursor #WIDTH * 0.5, 40
			moveCursor 0, 256 * %TOUCH_SCALE * 0.01
			setAlign #CENTER - %TOUCH_FLIP, #TOP
			touchView
			setAlign #CENTER + %TOUCH_FLIP, #TOP
			touchMove
		endif
		endif

		if %HEALTH <= 0
		if %INSTANTRESPAWN
		ifnot %SCOREBOARD
			setAlign #RIGHT, #TOP
			setCursor #WIDTH - 10 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 40
			setSize 128 * %TOUCH_SCALE * 0.01 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 128 * %TOUCH_SCALE * 0.01
			touchAttack
			setColorAlpha 0.3 + %TOUCH_ATTACK * 0.3
			drawPicByName "gfx/hud/keys/key_right"
		endif
		endif
		endif
	endif

	if %REALTEAM == #TEAM_SPECTATOR
		setAlign #RIGHT, #TOP
		setCursor #WIDTH - 10 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 10
		setSize 128 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 128
		touchAttack
		setColorAlpha 0.3 + %TOUCH_ATTACK * 0.3
		drawPicByName "gfx/hud/keys/act_cam"

		if %CHASING == #NOTSET
			setSize #WIDTH, #HEIGHT
			setCursor #WIDTH * 0.5, 148
			setAlign #CENTER - %TOUCH_FLIP, #TOP
			touchView
			setAlign #CENTER + %TOUCH_FLIP, #TOP
			touchMove
		endif
	endif
endif

if %MATCH_STATE >= #MATCH_STATE_WAITEXIT
	setAlign #RIGHT, #TOP
	setCursor #WIDTH - 10 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 10
	setSize 128 * 1.333 * %VIDHEIGHT / %VIDWIDTH, 128
	touchAttack
	setColorAlpha 0.3 + %TOUCH_ATTACK * 0.3
	drawPicByName "gfx/hud/keys/key_right"
endif

endif
